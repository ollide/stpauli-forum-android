apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "org.ollide.stpauliforum"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "0.1.0"
    }
    signingConfigs {
        release {
            keyAlias = "fcsp"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'

    // Dagger
    compile 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    compile 'javax.annotation:jsr250-api:1.0'

    // Network
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.9.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile("com.squareup.retrofit2:converter-simplexml:2.3.0") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Html parsing
    compile 'org.jsoup:jsoup:1.11.2'
    compile 'org.apache.commons:commons-lang3:3.7'

    // image loading
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Logging
    compile 'com.jakewharton.timber:timber:4.6.0'

    compile 'net.danlew:android.joda:2.9.9.1'
    testCompile 'joda-time:joda-time:2.9.9'
    testCompile 'commons-io:commons-io:2.6'

}

if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    // configuration missing, drop signingConfig
    android.buildTypes.release.signingConfig = null
}
